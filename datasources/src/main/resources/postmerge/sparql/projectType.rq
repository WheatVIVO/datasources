PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX p1:       <http://vivo.wheatinitiative.org/ontology/adminapp/>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX dcterms:  <http://purl.org/dc/terms/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresst:  <http://purl.org/net/OCRe/statistics.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX ocresp:   <http://purl.org/net/OCRe/study_protocol.owl#>
PREFIX ro:       <http://purl.obolibrary.org/obo/ro.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX swo:      <http://www.ebi.ac.uk/efo/swo/>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX vann:     <http://purl.org/vocab/vann/>

CONSTRUCT {
  ?x a ?projectType   
} WHERE {
  {
    ?x a vivo:Project
    BIND(<https://wheatvivo.org/ontology/extensions/CurrentProject> AS ?projectType)
    FILTER NOT EXISTS {
      ?x vivo:dateTimeInterval ?dti .
      ?dti vivo:end ?end
    }
    FILTER NOT EXISTS {
      ?someGrant vivo:fundingVehicleFor ?x .
      ?someGrant vivo:dateTimeInterval ?grantDti .
      ?grantDti vivo:end ?grantEnd
    }
  } UNION {
    ?x a vivo:Project
    BIND(<https://wheatvivo.org/ontology/extensions/CurrentProject> AS ?projectType)
    ?x vivo:dateTimeInterval ?dti .
    ?dti vivo:end ?end .
    ?end vivo:dateTime ?endDateTime .
    FILTER(?endDateTime >= NOW())
  } UNION {
    ?x a vivo:Project
    BIND(<https://wheatvivo.org/ontology/extensions/CurrentProject> AS ?projectType)
    FILTER NOT EXISTS {
      ?x vivo:dateTimeInterval ?projectDti .
      ?projectDti vivo:end ?projectEnd
    }
    ?grant vivo:fundingVehicleFor ?x .
    ?grant vivo:dateTimeInterval ?dti .
    ?dti vivo:end ?end .
    ?end vivo:dateTime ?endDateTime .
    FILTER(?endDateTime >= NOW())
  } UNION {
    ?x a vivo:Project
    BIND(<https://wheatvivo.org/ontology/extensions/CompletedProject> AS ?projectType)
    ?x vivo:dateTimeInterval ?dti .
    ?dti vivo:end ?end .
    ?end vivo:dateTime ?endDateTime .
    FILTER(?endDateTime < NOW())    
  } UNION {
    ?x a vivo:Project
    BIND(<https://wheatvivo.org/ontology/extensions/CompletedProject> AS ?projectType)
    FILTER NOT EXISTS {
      ?x vivo:dateTimeInterval ?projectDti .
      ?projectDti vivo:end ?projectEnd
    }
    ?grant vivo:fundingVehicleFor ?x .
    ?grant vivo:dateTimeInterval ?dti .
    ?dti vivo:end ?end .
    ?end vivo:dateTime ?endDateTime .
    FILTER(?endDateTime < NOW())    
  }
}