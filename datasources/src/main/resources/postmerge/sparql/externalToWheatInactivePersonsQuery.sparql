PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>

CONSTRUCT {
    ?x a <http://vivo.wheatinitiative.org/ontology/ExternalToWheat>
}
#SELECT (count(distinct ?x) AS ?count)
WHERE {
    # ignore people already tagged
    FILTER NOT EXISTS {
        ?x a <http://vivo.wheatinitiative.org/ontology/ExternalToWheat>
    }
    ?x a foaf:Person .
    # don't match persons with ORCID iDs
    FILTER NOT EXISTS { ?x vivo:orcidId ?orcidId }
    # don't match persons with roles from the Wheat Initiative website
    FILTER NOT EXISTS { ?x <http://www.wheatinitiative.org/administration/users/ontology/role> ?wiRole }
    # match persons with publications
    # but none in the last 10 years
    ?x vivo:relatedBy ?authorship .
    ?authorship a vivo:Authorship .
    FILTER NOT EXISTS {
        ?x vivo:relatedBy ?recentauthorship .
        ?recentauthorship a vivo:Authorship .
        ?recentauthorship vivo:relates ?recentpub .
        ?recentpub a bibo:Document .
        ?recentpub vivo:dateTimeValue ?recentdtv .
        ?recentdtv vivo:dateTime ?recentDateTime .
        BIND(xsd:integer(STRBEFORE(STR(?recentDateTime), "-")) AS ?recentYear)
        BIND(xsd:integer(STRBEFORE(STR(NOW()), "-")) AS ?currentYear)
         FILTER(?currentYear - ?recentYear <= 10)
    }
}
