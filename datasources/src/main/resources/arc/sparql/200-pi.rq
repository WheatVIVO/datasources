PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX arc:      <https://wheatvivo.org/ontology/arc/>
PREFIX g:        <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/>

CONSTRUCT {
    ?grant vivo:relates ?person .
    ?grant vivo:relates ?role .
    ?role a vivo:PrincipalInvestigatorRole .
    ?role obo:RO_0000052 ?person .
    ?role obo:BFO_0000054 ?project .
    ?person a foaf:Person .
    ?person rdfs:label ?personLabel .
    ?person obo:ARG_2000028 ?vcard .
    ?vcard a vcard:Individual .
    ?vcard vcard:hasName ?vcardName .
    ?vcardName a vcard:Name .
    ?vcardName vcard:familyName ?lastName .
    ?vcardName vcard:givenName ?firstName .
    ?vcard vcard:hasEmail ?vcardEmail .
    ?vcardUrl a vcard:Email, vcard:Work .
    ?vcardUrl vcard:email ?email .
} WHERE {
    ?grant vivo:fundingVehicleFor ?project .
    ?grant arc:Lead_InvestigatorSplit ?investigator .
    ?investigator g:firstName ?firstName .
    ?investigator g:lastName ?lastName .
    ?investigator g:rank ?rank .
    OPTIONAL {
      ?grant arc:Email_Address_of_Lead_Investigator ?email  
      BIND(IRI(CONCAT(STR(?grant), "-pi", STR(?rank), "-vcard-email")) AS ?vcardEmail) 
    }
    BIND(CONCAT(STR(?lastName), ", ", STR(?firstName)) AS ?personLabel)
    BIND(IRI(CONCAT(STR(?grant), "-piRole", STR(?rank))) AS ?role) 
    BIND(IRI(CONCAT(STR(?grant), "-pi", STR(?rank))) AS ?person) 
    BIND(IRI(CONCAT(STR(?grant), "-pi", STR(?rank), "-vcard")) AS ?vcard) 
    BIND(IRI(CONCAT(STR(?grant), "-pi", STR(?rank), "-vcard-name")) AS ?vcardName) 
}
