PREFIX vcard:   <http://www.w3.org/2006/vcard/ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ingest:  <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX bibo:    <http://purl.org/ontology/bibo/>
PREFIX obo:		<http://purl.obolibrary.org/obo/>
PREFIX vitro:   <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>


CONSTRUCT {

	# entity "has contact info" 
    ?pub obo:ARG_2000028 ?vcard .

    ?vcard vcard:hasURL ?url .
    ?url a vcard:URL .
    ?url vcard:url ?pubUrl .
    ?url rdfs:label ?pubUrlStr .

} WHERE {

    ?pub a bibo:Document .
    ?pub ingest:children ?child .
    ?child ingest:instance ?inst .
    ?inst ingest:webresource ?res .
    ?res ingest:url ?pubUrlStr .
    ?res vitro:position ?position .
    
    BIND ( xsd:anyURI( ?pubUrlStr ) AS ?pubUrl )
    BIND(IRI(CONCAT(STR(?pub), "-vcard")) AS ?vcard)
    BIND(IRI(CONCAT(STR(?pub), "-vcard-url-", STR(?position))) AS ?url)
}
