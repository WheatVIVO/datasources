PREFIX cordis:	<http://cordis.europa.eu/>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX vivo:	<http://vivoweb.org/ontology/core#>
PREFIX vcard:   <http://www.w3.org/2006/vcard/ns#>



CONSTRUCT {

	?project a vivo:Project .
	?project rdfs:label ?title .
	?project vivo:description ?objective .
	?project vivo:identifier ?project_id .
	
	?grant a vivo:Grant .
    ?grant rdfs:label ?title .
    ?grant vivo:description ?objective .
    ?grant vivo:identifier ?grant_id .
    ?grant vivo:totalAwardAmount ?project_cost .
    
    ?grant vivo:fundingVehicleFor ?project .
    
} WHERE {

	?project a cordis:project .
	OPTIONAL { ?project cordis:acronym ?acronym }
	?project cordis:title ?cordis_title .
	
    BIND( CONCAT( ?acronym, " - ", ?cordis_title ) AS ?title )
    
	OPTIONAL { ?project cordis:objective ?objective }
	
	OPTIONAL { ?project cordis:id ?project_id }
        BIND(CONCAT("grant-", STR(?project_id)) AS ?grant_id) 	
	BIND(IRI(CONCAT(STR(?project), "-grant")) AS ?grant)
	OPTIONAL { ?project cordis:totalCost ?project_cost }
}
