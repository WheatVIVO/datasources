PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX rcuk:     <http://gtr.rcuk.ac.uk/gtr/api/>
PREFIX rcukp:    <http://gtr.rcuk.ac.uk/gtr/api/person/>
PREFIX rcuko:    <http://gtr.rcuk.ac.uk/gtr/api/organisation/>

CONSTRUCT {
    ?x obo:ARG_2000028 ?vcard .
    ?vcard a vcard:Organization .
    ?vcard vcard:hasAddress ?vcardaddress .
    ?vcardaddress a vcard:Address .
    ?vcardaddress vcard:streetAddress ?streetAddress .
    ?vcardaddress vcard:locality ?locality .
    ?vcardaddress vcard:region ?region .
    ?vcardaddress vcard:postalCode ?postalCode .
    ?vcardaddress vcard:country ?country .
} WHERE {
    ?x a rcuko:organisation .
    ?x rcuko:addresses ?addresses .
    ?addresses rcuk:address ?address .
    OPTIONAL { ?address rcuk:line1 ?line1 }
    OPTIONAL { ?address rcuk:line2 ?line2 }
    OPTIONAL { ?address rcuk:line3 ?line3 }
    OPTIONAL { ?address rcuk:line4 ?line4 }
    BIND(COALESCE(CONCAT(" <br/> ", ?line2), "") AS ?lineTwo)
    BIND(COALESCE(CONCAT(" <br/> ", ?line3), "") AS ?lineThree)
    BIND(COALESCE(CONCAT(" <br/> ", ?line4), "") AS ?lineFour)
    BIND(concat(?line1, ?lineTwo, ?lineThree, ?lineFour) AS ?streetAddress)
    OPTIONAL { ?address rcuk:city ?locality }
    OPTIONAL { ?address rcuk:county ?region }
    OPTIONAL { ?address rcuk:postCode ?postalCode }
    OPTIONAL { ?address rcuk:country ?country }
    BIND(IRI(CONCAT(STR(?x), "-vcard")) AS ?vcard)
    BIND(IRI(CONCAT(STR(?x), "-vcard-address")) AS ?vcardaddress)

}
